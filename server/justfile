set export

PORT := "8080"

# List available commands
default:
    just --list --unsorted

# Run server in dev mode
dev:
    #!/bin/zsh

    uv run uvicorn src.buddy.main:app --host 0.0.0.0 --port $PORT

# Install modules
install-modules:
    #!/bin/zsh

    . ~/.zshrc

    uv sync

# Type check
type-check:
    uv run mypy .

# Format code
format:
    uv run ruff format .

# Lint code
lint:
    uv run ruff check .

# Lint and fix any issues that can be fixed automatically
lint-fix:
    uv run ruff check . --fix

# Prepare project to work with
prepare: install-modules

# Bootstrap project
bootstrap: install-uv prepare

[private]
install-uv:
    #!/bin/zsh

    if ! which uv > /dev/null
    then
        echo "Installing uv"
        curl -LsSf https://astral.sh/uv/install.sh | sh
    fi
